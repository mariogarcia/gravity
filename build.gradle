/*
 * Copyright 2008-2009 the original author or authors.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

def groovyProjects = [":modules:gravity-core"]
def grailsProjects = [":modules:gravity-grails",":modules:gravity-sample"]

buildscript {
	repositories {
		mavenCentral()
		maven { url 'http://repo.grails.org/grails/repo' }
	}
	dependencies {
		classpath "org.grails:grails-gradle-plugin:2.0.0-SNAPSHOT"
	}
}

/**
 * Common configuration
**/
allprojects{

 /* Artifact information */
	group = "org.grails.plugins"
	version = "1.0.0-SNAPSHOT"

	ext{
 	 /* Using version_ syntax to not interfere with
	    some other variables */
		version_groovy = "1.8.6"
		version_grails = "2.0.1"
	}

 /* Where to look for missing dependencies */
	repositories{
		mavenCentral()
		maven { 
			url 'http://repo.grails.org/grails/repo' 
		}
	}
}

subprojects{

	/**
 	 * Common configuration for Grails modules
	**/
	if (path in grailsProjects){
	 /* Setting the grails plugin */
		apply plugin: "grails"

	 /* Setting the grails version to use */
		grails {
			grailsVersion version_grails 
		}

	 /* Some desirable excludes */
		configurations {
			all {
				exclude module: "commons-logging"
				exclude module: 'xml-apis'
			}
			test {
				exclude module: "groovy-all"
			}
			compile{}
			releaseBootstrap.extendsFrom bootstrap
		}

	 /* Common dependencies for our grails projects */
		dependencies {
			compile "org.grails:grails-plugin-tomcat:${version_grails}"
			compile "org.grails:grails-plugin-datasource:${version_grails}"
			compile "org.grails:grails-plugin-services:${version_grails}"
			compile "org.grails:grails-hibernate:${version_grails}"

			bootstrap "org.codehaus.groovy:groovy-all:$version_groovy"
			bootstrap "org.grails.plugins:hibernate:$version_grails"
			bootstrap "org.grails.plugins:tomcat:$version_grails"
			bootstrap "org.grails.plugins:release:2.0.4"
		}

	}
}


