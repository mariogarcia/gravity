
import org.grails.gradle.plugin.GrailsTask

buildscript {
	repositories {
		mavenCentral()
		maven { 
			url 'http://repo.grails.org/grails/repo' 
		}
	}
	dependencies {
		classpath "org.grails:grails-gradle-plugin:2.0.0-SNAPSHOT"
	}
}

apply plugin: "grails"

grails {
	grailsVersion "2.0.1"
}

configurations {
	all {
		exclude module: "commons-logging"
		exclude module: 'xml-apis'
	}
	test {
		exclude module: "groovy-all"
	}
	releaseBootstrap.extendsFrom bootstrap
}

repositories {
	maven { url 'http://repo.grails.org/grails/repo' }
}

dependencies {
	["plugin-tomcat", "plugin-datasource","plugin-services", "hibernate"].each { module ->
		compile "org.grails:grails-$module:$grails.grailsVersion"
	}
	compile "org.grails.plugins:geb:0.9.0-RC-1"
	test project(':gravity-core')

	bootstrap "org.codehaus.groovy:groovy-all:$groovyVersion"
	bootstrap "org.grails.plugins:maven-publisher:0.8.1"

	[hibernate: grails.grailsVersion, tomcat: grails.grailsVersion, release: "2.0.4"].collect { name, version ->
		bootstrap "org.grails.plugins:$name:$version"
	}

}

task packagePlugin(type: GrailsTask) {
	command "package-plugin"
}

task dumpClassloaders(type: GrailsTask) {
	command "dump-classloaders"
}

test.dependsOn packagePlugin

task check(dependsOn: test)
task build(type: GrailsTask,dependsOn: [check, assemble]){
	command "maven-install"
}

